apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'jacoco-android'
apply plugin: 'com.github.dcendents.android-maven'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdkVersion 10
        targetSdkVersion rootProject.ext.compileSdkVersion
        versionCode 1
        versionName "1.0"

    }
    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

tasks.withType(Test) {
    systemProperties = System.properties
    systemProperties['user.dir'] = workingDir
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'org.mockito:mockito-all:1.10.19'
    testCompile 'junit:junit:4.12'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

group = project.properties['BINTRAY_GROUP'] ?: 'com.appprova'
version = project.properties['BINTRAY_VERSION'] ?: '1.0.0'

bintray {
    user = project.properties['BINTRAY_USER'] ?: 'test'
    key = project.properties['BINTRAY_PASSWORD'] ?: 'test'

    configurations = ['archives']

    pkg {
        repo = 'maven'
        name = project.name
        licenses = ['MIT']
        vcsUrl = 'https://github.com/guitcastro/DarylAndroidTracker'
    }
}
